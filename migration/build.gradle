apply plugin: 'com.android.library'
apply plugin: 'com.novoda.bintray-release'

android {
    compileSdkVersion 24
    buildToolsVersion '24.0.3'
    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 24
        versionCode 1
        versionName rootProject.ext.metadata.version
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            debuggable true
            testCoverageEnabled true
        }
        release {
            debuggable false
        }
    }
    productFlavors {
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    lintOptions {
        textReport true
        textOutput 'stdout'
    }
    dexOptions {
        preDexLibraries = !System.getenv("CI")
    }
}

final PACKAGE = 'com.github.gfx.android.orma.migration.sqliteparser.g'
final PACKAGE_PATH = PACKAGE.replaceAll(/\./, '/')
final GRAMMAR_FILE = project.file("src/main/antlr/SQLite.g4")
final ANTLR_OUTPUT_DIR = "$buildDir/generated/source/antlr"

configurations {
    antlr
}

android.sourceSets.main.java.srcDir ANTLR_OUTPUT_DIR

task generateParserSources(type: JavaExec) {
    description = 'Generate SQLiteParser sources with ANTLR4'
    main = 'org.antlr.v4.Tool'
    inputs.file GRAMMAR_FILE
    outputs.dir "$ANTLR_OUTPUT_DIR/$PACKAGE_PATH"
    // See https://github.com/antlr/antlr4/blob/master/doc/tool-options.md for details
    args = [GRAMMAR_FILE, '-o', "$ANTLR_OUTPUT_DIR/$PACKAGE_PATH", '-package', PACKAGE, '-Werror', '-long-messages']
    classpath = configurations.antlr

    doLast {
        delete fileTree(dir: "$ANTLR_OUTPUT_DIR/$PACKAGE_PATH", include: '*.tokens')
    }
}

tasks.preBuild.dependsOn(generateParserSources)

task updateSqliteGrammar(type: Exec) {
    final url = "https://raw.githubusercontent.com/bkiers/sqlite-parser/master/src/main/antlr4/nl/bigo/sqliteparser/SQLite.g4"
    commandLine "curl", "-L", url, "-o", GRAMMAR_FILE
}

dependencies {
    antlr 'org.antlr:antlr4:4.5.3'
    compile('org.antlr:antlr4-runtime:4.5.3')
    compile "com.android.support:support-annotations:${SUPPORT_LIBRARY_VERSION}"
    testCompile 'com.github.gfx.android.robolectricinstrumentation:robolectric-instrumentation:3.1.2-1'
    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'junit:junit:4.12'
    androidTestCompile 'org.hamcrest:hamcrest-library:1.3'
}

publish {
    artifactId = 'orma-migration'
    desc = 'A smart SQLiteDatabase migration engine'

    def metadata = rootProject.ext.metadata
    groupId = metadata.groupId
    publishVersion = metadata.version
    website = metadata.website
    repository = metadata.repository
    licences = metadata.licences
}
